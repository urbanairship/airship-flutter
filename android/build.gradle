group 'com.airship.airship'
version '1.0-SNAPSHOT'

buildscript {  
    ext.kotlin_version = '1.9.0'
    ext.coroutine_version = '1.5.2'
    ext.datastore_preferences_version = '1.1.1'
    ext.airship_framework_proxy_version = '14.2.1'


    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'org.yaml:snakeyaml:1.17'
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

def isHmsEnabled() {
    return (rootProject.hasProperty("airshipHmsEnabled") && rootProject.getProperty("airshipHmsEnabled") == "true") || (rootProject.ext.has("airshipHmsEnabled") && rootProject.ext.get("airshipHmsEnabled") == "true")
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 34

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    namespace = 'com.airship.airship'
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    api "com.urbanairship.android:airship-framework-proxy:$airship_framework_proxy_version"
    if (isHmsEnabled()) {
        implementation "com.urbanairship.android:airship-framework-proxy-hms:$airship_framework_proxy_version"
    }
    implementation "androidx.datastore:datastore-preferences:$datastore_preferences_version"
}
